from os import listdir
from os.path import isfile, join

in_dir = "/home/gk/analysis/intrahost/raw_reads/invivo_samples"
out_dir = "/home/gk/analysis/2018.11.21.1542865383.ZI.invivo"
bed = "/home/gk/db/zika/amplicons/zika_primers.bed"
ref="/home/gk/db/zika/PRVABC59.fa"
bwa_index="/home/gk/db/zika/PRVABC59"
pair_information="/home/gk/db/zika/amplicons/pair_information.tsv"

# File name format of replicates - ZI26_a_R{1-2}.fastq.gz ZI26_b_R{1-2}.fastq.gz
lst = listdir(in_dir)
files = [f for f in lst  if isfile(join(in_dir, f)) and ".fastq.gz" in f and "08_a" not in f and "08_b" not in f]
files.sort()

SAMPLES = []
for i in files:
    SAMPLES.append(i.split("_")[0])
SAMPLES = list(set(SAMPLES))

print(SAMPLES)

REPS=["a","b"]

rule all:
    input:
        expand("{out_dir}/masked_variants/{sample}.masked.filtered.tsv", out_dir = out_dir, sample = SAMPLES)

rule filter_variants:
    input:
        expand("{out_dir}/masked_variants/{{sample}}_{rep}.masked.tsv", out_dir=out_dir, rep=REPS)
    output:
        "{out_dir}/masked_variants/{sample}.masked.filtered.tsv"
    shell:
        "mkdir -p $(dirname {output})/ &&"
        "ivar filtervariants -p {output} {input}"

rule call_variants_post_removal:
    input:
        "{out_dir}/masked/{sample}_{rep}.masked.sorted.bam",
        "{ref}".format(ref=ref)
    output:
        "{out_dir}/masked_variants/{sample}_{rep}.masked.tsv"
    shell:
        "mkdir -p $(dirname {output})/ &&"
        "samtools mpileup -A -d 0 --reference {input[1]} -Q 0 -F 0 {input[0]} | ivar variants -p {output} -t 0.03"

rule remove_reads:
    input:
        "{out_dir}/trimmed/{sample}_{rep}.trimmed.sorted.bam",
        "{out_dir}/{sample}_masked_primer_indices.txt"
    output:
        "{out_dir}/masked/{sample}_{rep}.masked.sorted.bam"
    shell:
        "mkdir -p $(dirname {output})/ &&"
        "ivar removereads -i {input[0]} -p {wildcards.out_dir}/masked/{wildcards.sample}_{wildcards.rep}.masked -t {input[1]} && "
        "samtools sort -T {wildcards.sample}_{wildcards.rep} -o {output} {wildcards.out_dir}/masked/{wildcards.sample}_{wildcards.rep}.masked.bam && "
        "samtools index {output}"

rule get_masked:
    input:
        "{out_dir}/variants/{sample}.filtered.tsv",
        "{b}".format(b = bed),
        "{pair_information}".format(pair_information = pair_information)
    output:
        "{out_dir}/{sample}_masked_primer_indices.txt"
    shell:
        "ivar getmasked -i {input[0]} -b {input[1]}  -f {input[2]} -p {output}"

rule filter_variants_before_masking:
    input:
        expand("{{out_dir}}/variants/{{sample}}_{rep}.tsv", rep=REPS)
    output:
        "{out_dir}/variants/{sample}.filtered.tsv"
    shell:
        "mkdir -p $(dirname {output})/ &&"
        "ivar filtervariants -p {output} {input}"

rule call_variants:
    input:
        "{out_dir}/trimmed/{sample}.trimmed.sorted.bam",
        "{ref}".format(ref = ref)
    output:
        "{out_dir}/variants/{sample}.tsv"
    shell:
        "mkdir -p $(dirname {output})/ &&"
        "samtools mpileup -A -d 0 --reference {input[1]} -Q 0 -F 0 {input[0]} | ivar variants -p {wildcards.out_dir}/variants/{wildcards.sample} -t 0.03"

rule trim_primer_quality:
    input:
        "{out_dir}/aligned/{sample}.sorted.bam",
        "{b}".format(b = bed)
    output:
        "{out_dir}/trimmed/{sample}.trimmed.sorted.bam"
    shell:
        "mkdir -p {wildcards.out_dir}/trimmed/ && "
        "ivar trim -b {input[1]} -p {wildcards.out_dir}/trimmed/{wildcards.sample}.trimmed -i {input[0]} &&"
        "samtools sort -T {wildcards.sample} -o {output} {wildcards.out_dir}/trimmed/{wildcards.sample}.trimmed.bam &&"
        "samtools index {output}"

rule align_reads:
    input:
        expand("{in_dir}/{{sample}}_R{num}.fastq.gz", in_dir = in_dir, num=[1,2])
    params:
        index="{i}".format(i = bwa_index)
    output:
        "{out_dir}/aligned/{sample}.sorted.bam"
    shell:
        "mkdir -p $(dirname {output})/ && "
        "bwa mem {params.index} {input[0]} {input[1]} | samtools view -F 4 -Sb | samtools sort -T {wildcards.sample} -o {output} && "
        "samtools index {output}"
